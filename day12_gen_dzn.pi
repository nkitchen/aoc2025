import aoc.
import os.
import util.
import sat.

main([File]) =>
    go(File).

go(File) =>
    Input = read_file_lines(File),
    [Shapes, Regions] = parse_presents(Input),
    part1(Shapes, Regions, File),
    true.

parse_presents(Lines) = [Shapes, Regions] =>
    Blocks = line_blocks(Lines),
    Shapes = map(parse_shape, Blocks[1..length(Blocks)-1]),
    Regions = map(parse_region, last(Blocks)).

line_blocks(Lines) = Blocks =>
    LinesA = to_array(Lines),
    BlankIdxs = [0] ++ [I : I in 1..length(LinesA), LinesA[I] == ""] ++ [LinesA.length + 1],
    BlockDelims = window2(BlankIdxs),
    Blocks = [to_list(LinesA[I+1..J-1]) : [I, J] in BlockDelims].

parse_shape(Lines) = Shape =>
    % We don't need the line with the shape index.
    L = [map(shape_cell, Line) : Line in Lines.slice(2)],
    Shape = list_matrix_to_array_matrix(L).

shape_cell('.') = 0.
shape_cell('#') = 1.

parse_region(Line) = [Dims, ShapeCounts] =>
    find(Line, ": ", ColonFrom, ColonTo),
    DimsStr = Line[1..(ColonFrom - 1)],
    CountsStr = Line.slice(ColonTo + 1),
    Dims = reverse(map(to_number, DimsStr.split("x"))),
    ShapeCounts = map(to_number, CountsStr.split()).

part1(Shapes, Regions, File) =>
    Variants = map(make_variants, Shapes),
    foreach (R in 1..length(Regions))
        generate(R, Regions[R], Variants, File)
    end,
    true.

make_variants(Shape) = Variants =>
    Turned = [Shape],
    while (Turned.length < 4)
        Turned := [turn(first(Turned))|Turned]
    end,
    WithHFlips = flatten([[V, flip_horiz(V)] : V in Turned]),
    WithVFlips = flatten([[V, flip_vert(V)] : V in WithHFlips]),
    Seen = new_set(),
    Variants = [],
    foreach (V in WithVFlips)
        S = to_string(V),
        if (not Seen.has_key(S))
            Variants := [V|Variants],
            Seen.put(S)
        end
    end.

turn(V) = T =>
    M = V.length,
    N = V[1].length,
    T = {{V[I, J] : I in M..-1..1} : J in 1..N}.
flip_horiz(V) = map_array(reverse, V).
flip_vert(V) = V.transpose().flip_horiz().transpose().

generate(IRegion, Region, Variants, InputFile) =>
    DznName = to_fstring("%s.%d.dzn", InputFile, IRegion),
    Dzn = open(DznName, write),

    S = Variants.length,
    [[M, N], ShapeCounts] = Region,
    ShapeCounts.length == S,
    K = length(Variants[1, 1]),
    K == 3,

    printf(Dzn, "S = %d;\n", S),
    printf(Dzn, "M = %d;\n", M),
    printf(Dzn, "N = %d;\n", N),

    printf(Dzn, "ShapeCounts = %s;\n", to_string(ShapeCounts)),
    printf(Dzn, "NumVariants = %s;\n", to_string(map(length, Variants))),

    ShapeId = new_array(S, max(ShapeCounts)),
    NextShapeId = 1,
    foreach (I in 1..S)
        ShapeId[I] = new_array(max(ShapeCounts)),
        foreach (J in 1..length(ShapeId[I]))
            if (J <= ShapeCounts[I])
               ShapeId[I, J] := NextShapeId,
               NextShapeId := NextShapeId + 1
            else
               ShapeId[I, J] := 0
            end
        end
    end,
    MaxShapeId = NextShapeId - 1,
    printf(Dzn, "ShapeId = array2d(1..%d, 1..%d, %s);\n", S, max(ShapeCounts),
           to_string(flatten(array_matrix_to_list_matrix(ShapeId)))),

    printf(Dzn, "ShapeType = %s;\n", to_string([I : I in 1..S, J in 1..length(ShapeId[I]), ShapeId[I, J] > 0])),

    printf(Dzn, "ShapeInst = %s;\n", to_string([J : I in 1..S, J in 1..length(ShapeId[I]), ShapeId[I, J] > 0])),

    MaxVariants = max(map(length, Variants)),
    printf(Dzn, "MaxVariants = %d;\n", MaxVariants),
    printf(Dzn, "Variants = array4d(1..%d, 1..%d, 1..%d, 1..%d, [",
           S, MaxVariants, K, K),
    foreach (IShape in 1..S, V in 1..MaxVariants, DI in 1..K, DJ in 1..K)
        if ([IShape, V, DI, DJ] != [1, 1, 1, 1])
           print(Dzn, ", ")
        end,
        if (V <= length(Variants[IShape]))
           print(Dzn, Variants[IShape, V, DI, DJ])
        else
           print(Dzn, 0)
        end,
        if (DJ == K)
           print(Dzn, "\n")
        end
    end,
    print(Dzn, "]);\n"),

    close(Dzn),

    true.

% ===> multitime results (with GLPK installed)
% 1: picat day10.pi in10.txt
%             Mean        Std.Dev.    Min         Median      Max
% real        2.204       0.000       2.204       2.204       2.204
% user        1.299       0.000       1.299       1.299       1.299
% sys         0.897       0.000       0.897       0.897       0.897
