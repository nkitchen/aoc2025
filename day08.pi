import os.
import ordset.
import util.

main([File, ConnsStr]) =>
    go(File, to_number(ConnsStr)).

go(File, Conns) =>
    Input = read_file_lines(File),
    Boxes = [map(to_number, split(Line, ",")) : Line in Input],
    part1(N, Boxes, Conns),
    true.

part1(N, Boxes, Conns) =>
    N = Boxes.length,
    Dists = [],
    foreach (I in 1..(N-1), J in (I+1)..N)
        % Insert by negative distance so that it's easy to pop the largest one.
        D = (-dist(Boxes[I], Boxes[J]), I, J),
        DistsIns := insert(Dists, D),
        if (DistsIns.length > Conns)
            [DMax|DT] = DistsIns,
            Dists := DT
        else
            Dists := DistsIns
        end
    end,
    CircuitReps = new_map(),
    foreach ((_, I, J) in Dists)
        join(I, J, CircuitReps)
    end,
    BoxCircuits = [(Circuit, I) : I in 1..N, find_circuit(I, CircuitReps, Circuit)],
    CircuitSizes = [G.length : (Circuit, G) in grouped(sort(BoxCircuits))],
    Largest = take(reverse(sort(CircuitSizes)), 3),
    printf("Part 1: %ld\n", prod(Largest)),
    true.

dist(C1@[X1, Y1, Z1], C2@[X2, Y2, Z2]) = sqrt((X1 - X2)**2 +
                                              (Y1 - Y2)**2 +
                                              (Z1 - Z2)**2).

join(I, J, CircuitReps) =>
    find_circuit(I, CircuitReps, CI),
    find_circuit(J, CircuitReps, CJ),
    if (CI != CJ)
        CircuitReps.put(max(CI, CJ), min(CI, CJ))
    end.

find_circuit(I, CircuitReps, C) =>
    if (CircuitReps.has_key(I))
        D = CircuitReps.get(I),
        find_circuit(D, CircuitReps, C),
        if (D != C)
            CircuitReps.put(I, C)
        end
    else
        C = I
    end.

grouped([(C, I)]) = [(C, [I])].
grouped([(C, I)|T]) = G =>
    H = grouped(T),
    [(D, Js)|U] = H,
    if (C == D)
        G = [(C, [I|Js])|U]
    else
        G = [(C, [I])|H]
    end.
        
%===> multitime results
%1: picat day07.pi in07.txt
%            Mean        Std.Dev.    Min         Median      Max
%real        0.072       0.000       0.072       0.072       0.072
%user        0.052       0.000       0.052       0.052       0.052
%sys         0.019       0.000       0.019       0.019       0.019
