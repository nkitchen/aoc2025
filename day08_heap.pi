import os.
import util.

main([File]) =>
    println("Usage: picat day08.txt DATA CONNS").

main([File, ConnsStr]) =>
    go(File, to_number(ConnsStr)).

go(File, Conns) =>
    Input = read_file_lines(File),
    Boxes = [map(to_number, split(Line, ",")) : Line in Input],
    part1(Boxes, Conns),
    part2(Boxes, Conns),
    true.

part1(Boxes, Conns) =>
    N = Boxes.length,
    Dists = new_max_heap(Conns),
    foreach (I in 1..(N-1), J in (I+1)..N)
        D = (dist(Boxes[I], Boxes[J]), I, J),
        Dists.heap_push(D),
        if (Dists.heap_size() > Conns)
            _ = Dists.heap_pop()
        end
    end,
    CircuitReps = new_map(),
    foreach ((_, I, J) in Dists.heap_to_list())
        join(I, J, CircuitReps)
    end,
    BoxCircuits = [(Circuit, I) : I in 1..N, find_circuit(I, CircuitReps, Circuit)],
    CircuitSizes = [G.length : (Circuit, G) in grouped(sort(BoxCircuits))],
    Largest = take(reverse(sort(CircuitSizes)), 3),
    printf("Part 1: %ld\n", prod(Largest)),
    true.

part2(Boxes, Conns) =>
    N = Boxes.length,
    Dists = sort([(dist(Boxes[I], Boxes[J]), I, J) : I in 1..(N-1), J in (I+1)..N]),
    CircuitReps = new_map(),
    NCircuits = N,
    Last = (0, 0),
    foreach ((_, I, J) in Dists, break(NCircuits == 1))
        join(I, J, CircuitReps, Decr),
        NCircuits := NCircuits - Decr,
        Last := (I, J)
    end,
    (LastI, LastJ) = Last,
    FromWall = Boxes[LastI, 1] * Boxes[LastJ, 1],
    printf("Part 2: %ld\n", FromWall),
    true.

dist([X1, Y1, Z1], [X2, Y2, Z2]) = ((X1 - X2)**2 +
                                    (Y1 - Y2)**2 +
                                    (Z1 - Z2)**2).

join(I, J, CircuitReps) => join(I, J, CircuitReps, _).

join(I, J, CircuitReps, Decr) =>
    find_circuit(I, CircuitReps, CI),
    find_circuit(J, CircuitReps, CJ),
    if (CI != CJ)
        CircuitReps.put(max(CI, CJ), min(CI, CJ)),
        Decr = 1
    else
        Decr = 0
    end.

find_circuit(I, CircuitReps, C) =>
    if (CircuitReps.has_key(I))
        D = CircuitReps.get(I),
        find_circuit(D, CircuitReps, C),
        if (D != C)
            CircuitReps.put(I, C)
        end
    else
        C = I
    end.

grouped([(C, I)]) = [(C, [I])].
grouped([(C, I)|T]) = G =>
    H = grouped(T),
    [(D, Js)|U] = H,
    if (C == D)
        G = [(C, [I|Js])|U]
    else
        G = [(C, [I])|H]
    end.

%===> multitime results
%1: picat day08_heap.pi in08.txt 1000
%            Mean        Std.Dev.    Min         Median      Max
%real        11.213      0.000       11.213      11.213      11.213
%user        10.050      0.000       10.050      10.050      10.050
%sys         1.159       0.000       1.159       1.159       1.159
